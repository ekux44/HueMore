apply plugin: 'com.android.application'

android {
    useLibrary 'org.apache.http.legacy'
    lintOptions {
        checkReleaseBuilds false
    }
    signingConfigs {
        playRelease
    }
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    defaultConfig {
        applicationId "com.kuxhausen.huemore"
        versionCode 85 // version code in wear module must kept in sync with this
        versionName "3.2.4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            signingConfig signingConfigs.playRelease
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }
    productFlavors {
        playEdition {
            minSdkVersion 21
            targetSdkVersion 25
        }
        compatabilityEdition {
            minSdkVersion 21
            targetSdkVersion 25
        }
    }
    sourceSets {
        release.res.srcDirs = ['res', '../../trademarks']
    }
}

def Properties props = new Properties()
def propFile = file('../../signing.properties')
if (propFile.canRead()){
    props.load(new FileInputStream(propFile))

    if (props!=null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
        props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {

        android.signingConfigs.playRelease.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.playRelease.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.playRelease.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.playRelease.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
}else {
    println 'signing.properties not found'
    android.buildTypes.release.signingConfig = null
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:25.1.0'
    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:gridlayout-v7:18.0.0'
    compile 'com.android.support:design:25.1.0'
    compile 'com.android.support:preference-v14:25.1.0'
    compile 'com.larswerkman:HoloColorPicker:1.4'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
    compile project(':volley')
    compile project(':lifx')
    androidTestCompile 'com.android.support:support-annotations:25.1.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'org.mockito:mockito-core:2.6.9'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
}
